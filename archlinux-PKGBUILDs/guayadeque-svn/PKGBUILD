# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: sausageandeggs <sausageandeggs@archlinux.us>
# Contributor: Martin Herndl <martin.herndl@gmail.com>

_pkgname=guayadeque
pkgname=$_pkgname-svn
pkgver=1889
pkgrel=1
pkgdesc="Full Featured Linux media player for large collections"
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/$_pkgname'
license=('GPL3')
depends=('curl' 'desktop-file-utils' 'flac' 'libgpod' 'taglib' 'wxgtk2.8')
makedepends=('cmake' 'subversion')
optdepends=('gstreamer0.10-plugins: all Gstreamer plugins to play all kind of music files'
            'gvfs: Support for external devices')
provides=($_pkgname)
conflicts=($_pkgname)
install=$_pkgname.install
source=("$_pkgname::svn+https://$_pkgname.svn.sourceforge.net/svnroot/$_pkgname/Trunk"
        'CorrectRevision.patch')
md5sums=('SKIP'
         '6ea1d992ad1ce4b2da2fb95c651d9b6d')

pkgver() {
    cd "$SRCDEST/$_pkgname"
    svnversion | tr -d [A-z]
}

prepare() {
    cd "$srcdir/$_pkgname"
    patch -Np1 -i "$srcdir/CorrectRevision.patch"
}

build() {
    cd "$srcdir/$_pkgname"
    cmake . \
        -DCMAKE_BUILD_TYPE='Debug' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8' \
        -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8'\
        -D_GUREVISION_=$pkgver
    make
}

package() {
    cd "$srcdir/$_pkgname"
    make DESTDIR="$pkgdir" install
}