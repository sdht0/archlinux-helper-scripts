# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: sausageandeggs <sausageandeggs@archlinux.us>
# Contributor: Martin Herndl <martin.herndl@gmail.com>

_pkgname=guayadeque
pkgname=$_pkgname-svn
pkgver=1894
pkgrel=1
pkgdesc="Full Featured Linux media player for large collections"
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/$_pkgname'
license=('GPL3')
depends=('curl' 'desktop-file-utils' 'flac' 'libgpod' 'taglib' 'wxgtk2.8')
makedepends=('cmake' 'subversion' 'ninja')
optdepends=('gstreamer0.10-plugins: all Gstreamer plugins to play all kind of music files'
            'gvfs: Support for external devices')
provides=($_pkgname)
conflicts=($_pkgname)
install=$_pkgname.install
source=("$_pkgname::svn+http://svn.code.sf.net/p/$_pkgname/code"
        'CorrectlySetRevision.patch'
        'CMakeFix.patch')
md5sums=('SKIP' 'SKIP' 'SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    svnversion | tr -d [A-z]
}

prepare() {
    cd "$srcdir/$_pkgname"
    patch -Np1 -i "$srcdir/CorrectlySetRevision.patch"
    patch -Np1 -i "$srcdir/CMakeFix.patch"
}

build() {
    cd "$srcdir/$_pkgname/Trunk"

    CXXFLAGS="$CXXFLAGS -Wno-unused-local-typedefs"
    cmake . \
        -GNinja \
        -D_GUREVISION_=$pkgver \
        -DCMAKE_BUILD_TYPE='Debug' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8' \
        -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8'
    ninja
}

package() {
    cd "$srcdir/$_pkgname/Trunk"
    DESTDIR="$pkgdir" ninja install
}
