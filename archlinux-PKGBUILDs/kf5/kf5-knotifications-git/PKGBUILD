
pkgname=kf5-knotifications-git
_pkgname=knotifications
pkgver=8.740e9ab
pkgrel=1
pkgdesc="KNotifications"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/frameworks/knotifications"
license=('LGPL')
depends=('kf5-kwindowsystem-git' 'kde5-libdbusmenu-qt5-bzr')
makedepends=('extra-cmake-modules-git' 'git')
conflicts=("kf5-${_pkgname}" "${_pkgname}-git" "${_pkgname}")
provides=("kf5-${_pkgname}" "${_pkgname}")
source=("git://anongit.kde.org/knotifications")
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../"${_pkgname}" \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=DebugFull \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
