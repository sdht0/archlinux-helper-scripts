
pkgname=kf5-kwindowsystem-git
_pkgname=kwindowsystem
pkgver=13.11775f9
pkgrel=1
pkgdesc="KWindowSystem"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/frameworks/kwindowsystem"
license=('LGPL')
depends=('qt5-x11extras' 'libxfixes')
makedepends=('extra-cmake-modules-git' 'git')
conflicts=("kf5-${_pkgname}" "${_pkgname}-git" "${_pkgname}")
provides=("kf5-${_pkgname}" "${_pkgname}")
source=("git://anongit.kde.org/kwindowsystem")
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../"${_pkgname}" \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Debug \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
