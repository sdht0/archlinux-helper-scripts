# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde5-phonon-qt5-git
pkgver=v4.7.1.7.g2adbd9d
pkgrel=1
pkgdesc='The multimedia framework for KDE'
arch=('i686' 'x86_64')
url='http://phonon.kde.org/'
license=('LGPL')
depends=('qt5-base' 'phonon-backend' 'libpulse')
optdepends=('pulseaudio: PulseAudio support'
            'qt5-tools: QtDesigner Phonon plugin')
makedepends=('cmake-git' 'pulseaudio' 'git' 'qt5-quick1' 'qt5-tools')
source=(git://anongit.kde.org/phonon.git)
md5sums=('SKIP')

pkgver() {
  cd phonon
  git describe --always | sed 's|-|.|g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../phonon \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON \
    -DPHONON_BUILD_PHONON4QT5=ON \
    -DPHONON_INSTALL_QT_EXTENSIONS_INTO_SYSTEM_QT=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
