# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework-git
_pkgname=plasma-framework
pkgver=12061.160e97c
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/libs/plasma-framework'
license=('LGPL')
depends=('qt5-quick1' 'kf5-kidletime-git' 'kf5-kitemmodels-git'
         'kf5-kwidgetsaddons-git' 'kf5-kcodecs-git' 'kf5-karchive-git' 'kf5-solid-git' 'kf5-threadweaver-git'
         'kf5-kglobalaccel-git' 'kf5-kauth-git' 'kf5-kwallet-framework-git' 'kf5-kguiaddons-git'
         'kf5-kconfigwidgets-git' 'kf5-kitemviews-git' 'kf5-knotifications-git' 'kf5-kiconthemes-git'
         'kf5-kcompletion-git' 'kf5-kjobwidgets-git' 'kf5-sonnet-git' 'kf5-ktextwidgets-git' 'kf5-kxmlgui-git'
         'kf5-kbookmarks-git' 'kf5-kdeclarative-git' 'kf5-kunitconversion-git' 'kf5-kparts-git' 'kf5-kross-git'
         'kf5-kdnssd-framework-git' 'kf5-kde4support-git' 'kf5-kdesignerplugin-git' 'kf5-kinit-git'
         'kactivities-frameworks-git')
makedepends=('extra-cmake-modules-git' 'git' 'qt5-tools' 'kf5-kdoctools-git')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=('git://anongit.kde.org/plasma-framework')
md5sums=('SKIP')

pkgver() {
  cd plasma-framework
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  export CMAKE_PREFIX_PATH="/opt/kf5"

  cd build
  /opt/kf5/bin/cmake ../plasma-framework \
    -DCMAKE_BUILD_TYPE=DebugFull \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
