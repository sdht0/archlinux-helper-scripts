# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde5-runtime-git
pkgver=plasma2tp.r4.g50bf8d6
pkgrel=1
pkgdesc='Plugins and applications necessary for the running of KDE applications'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-runtime'
license=('LGPL')
depends=('qt5-x11extras' 'qt5-svg' 'openexr' 'exiv2' 'smbclient'
         'kde5-plasma-framework-git')
makedepends=('extra-cmake-modules-git' 'git' 'openslp')
source=('git://anongit.kde.org/kde-runtime.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kde-runtime
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../kde-runtime \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=/opt/kf5/lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
