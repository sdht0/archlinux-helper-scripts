# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kde5-akonadi-git
pkgver=v1.11.0.79.g48f56b9
pkgrel=1
arch=('i686' 'x86_64')
url='http://community.kde.org/KDE_PIM/Akonadi'
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
license=('LGPL')
depends=('shared-mime-info' 'boost-libs' 'mariadb' 'docbook-xsl')
makedepends=('cmake-git' 'boost' 'postgresql' 'git')
optdepends=('postgresql: PostgreSQL backend')
#install=akonadi.install
source=("git://anongit.kde.org/akonadi.git")
md5sums=('SKIP')

pkgver() {
  cd akonadi
  git describe --always | sed 's|-|.|g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../akonadi \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DAKONADI_BUILD_TESTS=OFF \
    -DQT5_BUILD=ON \
    -DWITH_SOPRANO=OFF \
    -DLIB_INSTALL_DIR=/opt/kf5/lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
