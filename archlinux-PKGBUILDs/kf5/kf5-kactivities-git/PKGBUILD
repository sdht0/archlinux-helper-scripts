
pkgname=kf5-kactivities-git
_pkgname=kactivities
pkgver=1
pkgrel=1
pkgdesc="Core components for the KDE's Activities

* Activity Manager
  System service to manage user's activities, track the usage patterns etc.
* KActivities library
  API for using and interacting with the Activity Manager as a consumer,
  application adding information to them or as an activity manager.
* Workspace
  Plugins for KDE workspace to easier integrate activities (KIO, etc.)"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/frameworks/kactivities"
license=('LGPL')
depends=('kf5-kservice-git' 'qt5-declarative' 'boost')
makedepends=('extra-cmake-modules-git' 'git')
group=("kf5")
conflicts=("${_pkgname}-git")
provides=("${_pkgname}-git")
source=("git://anongit.kde.org/kactivities")
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    mkdir -p build
}

build() {
    export PATH="/opt/kf5/bin:$PATH"
    export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"
    export LD_LIBRARY_PATH="/opt/kf5/lib:$LD_LIBRARY_PATH"
    export PKG_CONFIG_PATH="/opt/kf5/lib/pkgconfig:$PKG_CONFIG_PATH"
    export KDEDIRS="/opt/kf5"

    cd build
    cmake ../"${_pkgname}" \
        -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
        -DCMAKE_BUILD_TYPE=Debug \
        -DLIB_INSTALL_DIR=lib
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
