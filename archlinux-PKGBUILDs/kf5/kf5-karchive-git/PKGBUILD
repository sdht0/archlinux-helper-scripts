
pkgname=kf5-karchive-git
_pkgname=karchive
pkgver=1
pkgrel=1
pkgdesc="Qt 5 addon providing access to numerous types of archives"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/frameworks/karchive"
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules-git' 'git')
group=("kf5")
conflicts=("${_pkgname}-git")
provides=("${_pkgname}-git")
source=("git://anongit.kde.org/karchive")
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    mkdir -p build
}

build() {
    export PATH="/opt/kf5/bin:$PATH"
    export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"
    export LD_LIBRARY_PATH="/opt/kf5/lib:$LD_LIBRARY_PATH"
    export PKG_CONFIG_PATH="/opt/kf5/lib/pkgconfig:$PKG_CONFIG_PATH"
    export KDEDIRS="/opt/kf5"

    cd build
    cmake ../"${_pkgname}" \
        -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
        -DCMAKE_BUILD_TYPE=Debug \
        -DLIB_INSTALL_DIR=lib
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
