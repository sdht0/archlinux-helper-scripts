
pkgname=kde5-kio-extras-git
_pkgname=kio-extras
pkgver=1
pkgrel=1
pkgdesc="Additional components to increase the functionality of KIO"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/kde/workspace/kio-extras"
license=('LGPL')
depends=('kf5-kf5umbrella-git' 'openslp' 'samba' 'libssh' 'openexr' 'libkexiv2')
makedepends=('extra-cmake-modules-git' 'git')
group=("kde5")
conflicts=("${_pkgname}-git")
provides=("${_pkgname}-git")
source=("git://anongit.kde.org/kio-extras")
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    mkdir -p build
}

build() {
    export PATH="/opt/kf5/bin:$PATH"
    export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"
    export LD_LIBRARY_PATH="/opt/kf5/lib:$LD_LIBRARY_PATH"
    export PKG_CONFIG_PATH="/opt/kf5/lib/pkgconfig:$PKG_CONFIG_PATH"
    export KDEDIRS="/opt/kf5"

    cd build
    cmake ../"${_pkgname}" \
        -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
        -DCMAKE_BUILD_TYPE=Debug \
        -DLIB_INSTALL_DIR=lib
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
