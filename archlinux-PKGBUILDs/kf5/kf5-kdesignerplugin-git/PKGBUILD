
pkgname=kf5-kdesignerplugin-git
_pkgname=kdesignerplugin
pkgver=20.cf6639a
pkgrel=1
pkgdesc="KDesignerPlugin"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/frameworks/kdesignerplugin"
license=('LGPL')
depends=('kf5-kio-git' 'kf5-kplotting-git' 'kf5-kdewebkit-git')
makedepends=('extra-cmake-modules-git' 'git' 'qt5-tools' 'kf5-kdoctools-git')
conflicts=("kf5-${_pkgname}" "${_pkgname}-git" "${_pkgname}")
provides=("kf5-${_pkgname}" "${_pkgname}")
source=("git://anongit.kde.org/kdesignerplugin")
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  /opt/kf5/bin/cmake ../"${_pkgname}" \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=DebugFull \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
