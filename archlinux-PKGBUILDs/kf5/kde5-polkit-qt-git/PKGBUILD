# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde5-polkit-qt-git
pkgver=v0.103.0.7.g3a5f879
pkgrel=1
pkgdesc="Qt5 wrapper around polkit-1 client libraries, used by kdelibs to provide the polkit-1 kauth backend and by KDE's polkit client"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/polkit-qt-1'
license=('LGPL')
depends=('qt5-base' 'polkit')
makedepends=('extra-cmake-modules-git' 'git')
source=(git://anongit.kde.org/polkit-qt-1.git#branch=qt5)
md5sums=('SKIP')

pkgver() {
  cd polkit-qt-1
  git describe --always | sed 's|-|.|g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../polkit-qt-1 \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=DebugFull \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
