# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=extra-cmake-modules-git
_pkgname=extra-cmake-modules
pkgver=0.0.11.r2510.f686ff7
pkgrel=1
pkgdesc='Extra CMake modules'
arch=('any')
url='http://quickgit.kde.org/index.php?p=extra-cmake-modules.git&a=summary'
license=('LGPL')
depends=('cmake-git')
makedepends=('git')
source=(git://anongit.kde.org/extra-cmake-modules.git)
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  echo "0.0.11.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../extra-cmake-modules \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Debug
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

