# $Id
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde5-kactivities-frameworks-git
pkgver=586.acfce06
pkgrel=1
pkgdesc="Core components for the KDE's Activities"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('LGPL')
depends=('qt5-declarative' 'boost-libs' 'kf5-kf5umbrella-git')
makedepends=('extra-cmake-modules-git' 'git' 'boost')
source=('git://anongit.kde.org/kactivities.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kactivities
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../kactivities \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=/opt/kf5/lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
