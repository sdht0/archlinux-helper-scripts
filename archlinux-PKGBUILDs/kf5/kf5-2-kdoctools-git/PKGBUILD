
pkgname=kf5-kdoctools-git
_pkgname=kdoctools
pkgver=7.d156e6d
pkgrel=1
pkgdesc="KDocTools"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/frameworks/kdoctools"
license=('LGPL')
depends=('kf5-karchive-git' 'docbook-xsl')
makedepends=('extra-cmake-modules-git' 'git')
conflicts=("kf5-${_pkgname}" "${_pkgname}-git" "${_pkgname}")
provides=("kf5-${_pkgname}" "${_pkgname}")
source=("git://anongit.kde.org/kdoctools")
options=('staticlibs')
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../"${_pkgname}" \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Debug \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
