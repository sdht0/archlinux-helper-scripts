
pkgname=kf5-kxmlgui-git
_pkgname=kxmlgui
pkgver=15.b5e51bb
pkgrel=1
pkgdesc="KXMLGUI"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/frameworks/kxmlgui"
license=('LGPL')
depends=('kf5-kglobalaccel-git' 'kf5-ktextwidgets-git' 'kde5-attica-git')
makedepends=('extra-cmake-modules-git' 'git')
conflicts=("kf5-${_pkgname}" "${_pkgname}-git" "${_pkgname}")
provides=("kf5-${_pkgname}" "${_pkgname}")
source=("git://anongit.kde.org/kxmlgui")
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../"${_pkgname}" \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=DebugFull \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
