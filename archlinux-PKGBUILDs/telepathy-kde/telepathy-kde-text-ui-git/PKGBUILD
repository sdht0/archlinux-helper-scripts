# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-text-ui-git
_pkgname=ktp-text-ui
pkgver=0.7.r1548.c07a667
pkgrel=1
pkgdesc="Telepathy handler for Text Chats"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-contact-list-git' 'telepathy-logger-qt-git' 'qjson')
makedepends=('git' 'cmake' 'automoc4' 'boost')
groups=('kde-telepathy-git')
conflicts=('telepathy-kde-text-ui')
provides=('telepathy-kde-text-ui')
source=("git://anongit.kde.org/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.7.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p "$srcdir/build"
}

build() {
  cd "$srcdir/build"

  cmake ../$_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=DebugFull
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="${pkgdir}" install
}
