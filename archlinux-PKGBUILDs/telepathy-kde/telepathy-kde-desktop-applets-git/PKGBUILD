# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-desktop-applets-git
_pkgname=ktp-desktop-applets
pkgver=0.7.r759.f682609
pkgrel=1
pkgdesc="The KDE-Telepathy Plasma desktop applets"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-common-internals-git' 'kdebase-workspace')
makedepends=('git' 'cmake' 'automoc4')
groups=('kde-telepathy-git')
conflicts=('telepathy-kde-contact-applet' 'telepathy-kde-presence-applet' 'telepathy-kde-desktop-applets')
replaces=('telepathy-kde-contact-applet' 'telepathy-kde-presence-applet')
provides=('telepathy-kde-desktop-applets')
source=("git://anongit.kde.org/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.7.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p "$srcdir/build"
}

build() {
  cd "$srcdir/build"

  cmake ../$_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=DebugFull
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="${pkgdir}" install
}
