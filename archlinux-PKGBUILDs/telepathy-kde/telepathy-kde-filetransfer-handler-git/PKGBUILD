# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-kde-filetransfer-handler-git
_pkgname=ktp-filetransfer-handler
pkgver=0.6.80.5.gd592c28
pkgrel=1
pkgdesc="KDE Telepathy file transfer handler"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/kdereview/ktp-filetransfer-handler"
license=('GPL')
depends=('telepathy-kde-common-internals-git')
makedepends=('cmake' 'automoc4' 'git')
conflicts=('telepathy-kde-filetransfer-handler'
           'telepathy-filetransfer-handler-git')
provides=('telepathy-kde-filetransfer-handler')
source=("git://anongit.kde.org/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g;s|v||'
}

prepare() {
  mkdir -p "$srcdir/build"
}

build() {
  cd "$srcdir/build"

  cmake ../$_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=DebugFull
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="${pkgdir}" install
}
