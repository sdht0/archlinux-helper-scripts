# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-kde-contact-list-git
_pkgname=ktp-contact-list
pkgver=0.7.r905.2091c55
pkgrel=1
pkgdesc="KDE Telepathy contact list application"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/kdereview/ktp-contact-list"
license=('GPL')
depends=('telepathy-kde-common-internals-git')
makedepends=('git' 'cmake' 'automoc4')
conflicts=('telepathy-kde-contact-list'
           'telepathy-contact-list-git')
provides=('telepathy-kde-contact-list')
install=telepathy-kde-contact-list.install
source=("git://anongit.kde.org/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.7.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p "$srcdir/build"
}

build() {
  cd "$srcdir/build"

  cmake ../$_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=DebugFull
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="${pkgdir}" install
}
