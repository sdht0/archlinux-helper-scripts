# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-send-file-git
_pkgname=ktp-send-file
pkgver=0.7.0.2.g39cce82
pkgrel=1
pkgdesc="A File manager plugin to launch a file transfer job with a specified contact"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-filetransfer-handler-git' 'telepathy-kde-common-internals-git')
makedepends=('git' 'cmake' 'automoc4')
groups=('kde-telepathy-git')
install=telepathy-kde-send-file.install
conflicts=('telepathy-kde-send-file')
provides=('telepathy-kde-send-file')
source=("git://anongit.kde.org/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g;s|v||'
}

prepare() {
  mkdir -p "$srcdir/build"
}

build() {
  cd "$srcdir/build"

  cmake ../$_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=DebugFull
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="${pkgdir}" install
}
