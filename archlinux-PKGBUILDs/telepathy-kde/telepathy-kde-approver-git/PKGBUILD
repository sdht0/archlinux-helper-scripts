# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-approver-git
_pkgname=ktp-approver
pkgver=0.7.r196.548ab3b
pkgrel=1
pkgdesc="KDE Channel Approver for Telepathy"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-common-internals-git' 'telepathy-kde-contact-list-git')
makedepends=('git' 'cmake' 'automoc4')
groups=('kde-telepathy-git')
conflicts=('telepathy-kde-approver')
provides=('telepathy-kde-approver')
source=("git://anongit.kde.org/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.7.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p "$srcdir/build"
}

build() {
  cd "$srcdir/build"

  cmake ../$_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=DebugFull
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="${pkgdir}" install
}
