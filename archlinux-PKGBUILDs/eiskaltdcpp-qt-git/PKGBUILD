# Contributor: dhamp <dhamp@ya.ru>

_pkgname=eiskaltdcpp
pkgname=${_pkgname}-qt-git
pkgver=2.3.r4545.e383290
pkgrel=1
license=('GPL3')
arch=('i686' 'x86_64')
pkgdesc="Qt4-based DC and ADC client based on dcpp core"
url="http://code.google.com/p/eiskaltdc/"
depends=('qt4' 'miniupnpc' 'libnotify' 'zlib' 'bzip2' 'pcre' 'lua' 'libidn' 'openssl')
makedepends=('git' 'cmake' 'boost')
optdepends=('aspell: spellchecking'
            'qtscriptgenerator: need for qtscript')
conflicts=('eiskaltdcpp' 'eiskaltdcpp-qt')
provides=('eiskaltdcpp-qt')
options=(!emptydirs)
install=${_pkgname}.install
source=('git://github.com/eiskaltdcpp/eiskaltdcpp.git#branch=2.3.x')
sha1sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  echo "2.3.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
    cd ${srcdir}/${_pkgname}
    cmake . \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
        -DPERL_REGEX=ON \
        -DUSE_MINIUPNP=ON \
        -DLOCAL_MINIUPNP=ON \
        -DWITH_EXAMPLES=ON \
        -DWITH_SOUNDS=ON \
        -DLUA_SCRIPT=ON  \
        -DWITH_LUASCRIPTS=ON \
        -DWITH_DHT=ON \
        -DUSE_QT=ON \
        -DUSE_QT_QML=ON \
        -DUSE_JS=ON
    make
}

package() {
    cd ${srcdir}/${_pkgname}
    make DESTDIR=${pkgdir} install
}
