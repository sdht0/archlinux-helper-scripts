# Maintainer: Paul Weingardt <paulweingardt@web.de>
# Contributor: TDY <tdy@gmx.com>
pkgname=git-cola
pkgver=1.8.5
pkgrel=1
pkgdesc="A powerful GUI for Git"
arch=('any')
url="http://git-cola.github.com/"
license=('GPL')
depends=('git' 'python2-pyqt>=4.3')
makedepends=('asciidoc' 'docbook-xsl' 'rsync' 'xmlto' 'python2-sphinx>=1.1.3')
optdepends=('python2-pyinotify>=0.7.1: for inotify support'
'tk: to use the built-in ssh-askpass handler')
options=('!makeflags')
source=("http://git-cola.github.io/releases/$pkgname-$pkgver.tar.gz")
sha256sums=('b36da84c0310a151af9c04415eb8c14846c0bfb818f7b24e31ad50f932188300')

build() {
	cd "$srcdir/${pkgname}-$pkgver"
	sed -i 's,= python,&2,' Makefile
	sed -i 's,sphinx-build,&2,' share/doc/git-cola/Makefile
	sed -i 's,env python,&2,' setup.py

	make all doc html
}

package() {
	cd "$srcdir/${pkgname}-$pkgver"
	make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html}
}
