# Maintainer: Thomas Wei√üschuh <thomas t-8ch de>

pkgname=smlnj
pkgver=110.76
pkgrel=1
pkgdesc="Standard ML of New Jersey, a compiler for the Standard ML '97 programming language"
url="http://www.smlnj.org/"
license=(BSD)
arch=(i686 x86_64)
provides=(sml)

if [ "$CARCH" = "x86_64" ]; then
  makedepends=('gcc-multilib')
  depends=('lib32-glibc')
else
  # everything in base{,-devel}
  makedepends=()
  depends=()
fi

_url="http://smlnj.cs.uchicago.edu/dist/working/${pkgver}/"

source=(
  "urlgetter.sh"
  "profile.d-smlnj.sh"

  "smlnj-${pkgver}-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"

  "smlnj-${pkgver}-config.tgz::${_url}config.tgz"

  "smlnj-${pkgver}-cm.tgz::${_url}cm.tgz"
  "smlnj-${pkgver}-compiler.tgz::${_url}compiler.tgz"
  "smlnj-${pkgver}-runtime.tgz::${_url}runtime.tgz"
  "smlnj-${pkgver}-system.tgz::${_url}system.tgz"
  "smlnj-${pkgver}-MLRISC.tgz::${_url}MLRISC.tgz"
  "smlnj-${pkgver}-smlnj-lib.tgz::${_url}smlnj-lib.tgz"

  "smlnj-${pkgver}-ckit.tgz::${_url}ckit.tgz"
  "smlnj-${pkgver}-nlffi.tgz::${_url}nlffi.tgz"

  "smlnj-${pkgver}-cml.tgz::${_url}cml.tgz"

  "smlnj-${pkgver}-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "smlnj-${pkgver}-ml-lex.tgz::${_url}ml-lex.tgz"
  "smlnj-${pkgver}-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "smlnj-${pkgver}-ml-burg.tgz::${_url}ml-burg.tgz"

  "smlnj-${pkgver}-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
)

_unused=(
  "smlnj-${pkgver}-eXene.tgz::${_url}eXene.tgz"

  "smlnj-${pkgver}-pgraph.tgz::${_url}pgraph.tgz"
  "smlnj-${pkgver}-heap2asm.gz::${_url}heap.asm.tgz"
)

build() {

  cd "$srcdir"

  # The build system uses this env variable
  URLGETTER="$srcdir/urlgetter.sh"

  # urlgetter.sh uses these variables
  export srcdir
  export pkgver

  # Parallel builds won't work
  unset MAKEFLAGS

  # Confuses install.sh
  unset SMLNJ_HOME

  config/install.sh
}

package() {

  cd "$srcdir"

  mkdir -p "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/"{bin,lib} "$pkgdir/usr/lib/smlnj"

  mkdir -p "$pkgdir/etc/profile.d"
  cp "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
}

sha256sums=('4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            'dd16569fb6991d673e66dae57f5e9b372d5a4e305f92a9756002e4bb296c9b61'
            '41cec8bd28c43f49bea24cc7d80d091d8a0e0c7473ee67e4a0b06019567ca557'
            '539e8199764a803f486dbf5c0ecb3fa3b2277a6b52ccbbf350bbdcc231bec193'
            '322755b8a3f105eec4ce4a3197aaf83bcd542e4c2b2f43c724d0ae8f393bf681'
            'f6086cd4e30ea3a89c528ee08c793eca77f7701589e0436acf347faf7e940a7f'
            '8f6a143db22a9d025e2820899c9cccbf0e66c679a9aa68c3dcd1bec68003da37'
            '9d944dea4e80a8ef2cc01e7e16ae72cb280f2e4dc0d6648c9bc27156ccfcc102'
            '81e994a07d7c8979b28c4911272de3c37eec648e58c03a9dc1ecb9bbbadca1d7'
            'c4534e6e7f910ebee3146cc8ed214a1d5a2ea90afbd79c2290cefdc784ee0dbb'
            '07bdc123b2c93f01bc0a5bbdf33aac0b34407bd70cd8d3e1d06ad55e92deb521'
            'cbd357a3b3377d049911a247be880dcde7f52f705f7cdfe800b67631858681e1'
            '9970b22506a605012055700cc28497c2850d29fbb7bcc5b069fbef785220fc8d'
            '9d00143cff4e8cc87421270d34b29864c27d353d6b429cce3d3428f6c7280a6b'
            '9151f7fef0abcb312daf731621b682b2c7e9f87a151832e99c30b5e56f03f1cf'
            'd59dad9188778c96b3e59f9d754a4fbdef4b67de2ded67deb0d26c40915a594e'
            '3769e1d251d04c95b4c1b759f3bf21644bcb3135404d0346e8ff34553ae704df')
