# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>

# TODO:
# Nginx template files: lib/support/nginx/gitlab
#   https://github.com/gitlabhq/gitlab-recipes/blob/master/web-server/nginx/gitlab-ssl
#   https://github.com/gitlabhq/gitlab-recipes/blob/master/web-server/apache/gitlab.conf
#   https://github.com/gitlabhq/gitlab-recipes/blob/master/web-server/lighttpd/10-gitlab.conf
# git config global missing /var/run/gitlab/.gitconfig
# makechrootpkg fixes?
# confirm missing dependencies
# missing: https://github.com/gitlabhq/gitlab-recipes/blob/master/init/systemd/gitlab-unicorn.service

pkgname=gitlab
pkgver=6.3.0
pkgrel=1
pkgdesc="Project management and code hosting application"
arch=('any')
url="https://www.gitlab.org"
license=('MIT')
depends=('ruby' 'ruby-bundler' 'python2' 'gitlab-shell' 'postgresql-libs')
# not yet tested and confirmed: libxslt:
makedepends=('sudo' 'libxslt')
# base-devel zlib libyaml openssl gdbm readline ncurses libffi curl openssh redis libxml2 libxslt icu 
optdepends=(
    'mariadb: database backend'
    'postgresql: database backend'
    'python2-docutils: reStructuredText markup language support'
    'postifx: mail server in order to receive mail notifications'
)
backup=(etc/gitlab/gitlab.yml etc/gitlab/database.yml)
# etc/gitlab/reque.yml
source=("$pkgname-$pkgver.tar.gz::https://github.com/gitlabhq/gitlabhq/archive/v${pkgver}.tar.gz"
        "gitlab.service"
        "https://github.com/gitlabhq/gitlab-recipes/raw/master/init/systemd/gitlab-sidekiq.service")
install='gitlab.install'
sha512sums=("48e45c0927717c04e1c04614519f9022c47e367e502b7f8a0c725adadc332dc6b74a602b019fe5cecad0de69e50d1a6cf7c6ee8ffc973c04c644aae4a677dcf8"
            "f515a6a6b94b5c15405978addd2923dbfa4aaedd9eff2aa2760f53c3a88ec8148fb39b6bcb5897d5b0e586e44945475b1c3cd09350138d0ae7560aae504120dd"
            "97b713b54f8bf7caf40427f868c8286a5c8cb92c7b191ea1ea451430c9123077ab06d5908875a4be3ad6379f43de34a395e6602123eee79daf0734d06f7e3809")
options=('!strip')

prepare() {
        cd "$srcdir/gitlabhq-$pkgver"

        # Patching Systemd unit files:
        sed -i "s|WorkingDirectory=/home/git|WorkingDirectory=/usr/share/webapps|" ${srcdir}/gitlab-sidekiq.service
        sed -i "s|User=git|User=gitlab|" ${srcdir}/gitlab-sidekiq.service
        sed -i "s|/home/git|/var/lib|g" ${srcdir}/gitlab-sidekiq.service
        sed -i "s|/tmp/pids|/pids|g" ${srcdir}/gitlab-sidekiq.service
        sed -i "s|-P tmp/|-P /var/lib/gitlab/|" ${srcdir}/gitlab-sidekiq.service

}

build() {
        cd "$srcdir/gitlabhq-$pkgver"
        msg "Fetching bundled gems..."
        # Gems will be installed into vendor/bundle
        bundle install --no-cache --no-prune --deployment --without development test aws
}

package() {
    homedir="/var/lib/gitlab"
    datadir="/usr/share/webapps/gitlab"

    cd $srcdir/gitlabhq-$pkgver
    install -d "$pkgdir/usr/share/webapps"
	cp -r "${srcdir}/gitlabhq-${pkgver}" "${pkgdir}${datadir}"

    # Creating directories
    install -d \
        "${pkgdir}/etc/gitlab" \
        "${pkgdir}/usr/share/webapps" \
        "${pkgdir}/usr/share/doc/${pkgname}" \
        "${pkgdir}${homedir}/pids" \
        "${pkgdir}${homedir}/www" \
        "${pkgdir}${datadir}/www"
    
    # Install config files
    sed -e "s|# user: git|user: gitlab|" \
        -e "s|/home/git/repositories|$homedir/repositories|" \
        -e "s|/home/git/gitlab-satellites|$homedir/satellites|" \
        -e "s|/home/git/gitlab-shell|/usr/share/gitlab-shell|" \
        config/gitlab.yml.example > "$pkgdir/etc/gitlab/gitlab.yml"
    ln -s /etc/gitlab/gitlab.yml ${pkgdir}${datadir}/config/gitlab.yml
    cp config/database.yml.mysql $pkgdir/etc/gitlab/database.yml
    ln -s /etc/gitlab/database.yml ${pkgdir}${datadir}/config/database.yml

    # Install license, help and systemd service files
    mv README.md MAINTENANCE.md CONTRIBUTING.md CHANGELOG config/*.{example,mysql,postgresql} "$pkgdir/usr/share/doc/$pkgname"
    install -D "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm $pkgdir/usr/share/webapps/gitlab/LICENSE

    # Install systemd service files
    install -Dm0755 "$srcdir/gitlab.service" "$pkgdir/usr/lib/systemd/system/gitlab.service"
    install -Dm0755 "$srcdir/gitlab-sidekiq.service" "$pkgdir/usr/lib/systemd/system/gitlab-sidekiq.service"
}

# vim:set ts=4 sw=4 et:
