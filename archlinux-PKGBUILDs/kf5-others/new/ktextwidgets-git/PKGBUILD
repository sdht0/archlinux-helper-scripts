# $Id: PKGBUILD 202287 2013-12-21 09:59:45Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktextwidgets-git
pkgver=r5.081abfc
pkgrel=1
pkgdesc='KTextWidgets'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/ktextwidgets'
license=('LGPL')
depends=('kcompletion-git' 'kservice-git' 'kiconthemes-git' 'sonnet-git')
makedepends=('extra-cmake-modules-git' 'git')
groups=('kf5')
conflicts=(ktextwidgets)
provides=(ktextwidgets)
source=('git://anongit.kde.org/ktextwidgets.git')
md5sums=('SKIP')

pkgver() {
  cd ktextwidgets
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ktextwidgets \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
