# $Id: PKGBUILD 202380 2013-12-21 14:03:17Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kded-git
pkgver=r11.867d3c9
pkgrel=1
pkgdesc='KDE Daemon'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kded'
license=('LGPL')
depends=('kservice-git' 'kinit-git')
makedepends=('extra-cmake-modules-git' 'git' 'kdoctools-git')
groups=('kf5')
conflicts=(kded)
provides=(kded)
source=('git://anongit.kde.org/kded.git')
md5sums=('SKIP')

pkgver() {
  cd kded
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kded \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
