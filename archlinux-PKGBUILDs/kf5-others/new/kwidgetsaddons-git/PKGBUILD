# $Id: PKGBUILD 202357 2013-12-21 12:19:47Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwidgetsaddons-git
pkgver=r6.aa65281
pkgrel=1
pkgdesc='KWidgetsAddons'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kwidgetsaddons'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules-git' 'git')
groups=('kf5')
conflicts=(kwidgetsaddons)
provides=(kwidgetsaddons)
source=('git://anongit.kde.org/kwidgetsaddons.git')
md5sums=('SKIP')

pkgver() {
  cd kwidgetsaddons
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kwidgetsaddons \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
