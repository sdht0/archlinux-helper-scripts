# $Id: PKGBUILD 202287 2013-12-21 09:59:45Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcmutils-git
pkgver=r5.43b0062
pkgrel=1
pkgdesc='Utilities for interacting with KCModules'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kcmutils'
license=('LGPL')
depends=('kxmlgui-git')
makedepends=('extra-cmake-modules-git' 'git')
groups=('kf5')
conflicts=(kcmutils)
provides=(kcmutils)
source=('git://anongit.kde.org/kcmutils.git')
md5sums=('SKIP')

pkgver() {
  cd kcmutils
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kcmutils \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
