# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kemoticons
pkgver=4.95.0
pkgrel=1
pkgdesc='KEmoticons'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kemoticons'
license=('LGPL')
depends=('karchive' 'kservice')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('6a64b1d7206d4f7e59f05345d47979a7')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
