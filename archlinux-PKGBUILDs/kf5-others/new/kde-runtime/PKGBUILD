# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-runtime
pkgver=0.plasma2tp
pkgrel=1
pkgdesc='Plugins and applications necessary for the running of KDE applications'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-runtime'
license=('LGPL')
depends=('qt5-x11extras' 'qt5-svg' 'openexr' 'exiv2' 'smbclient'
         'kcmutils' 'kde4support' 'kemoticons'
         'kio' 'knewstuff' 'knotifyconfig' 'kunitconversion')
makedepends=('extra-cmake-modules' 'git' 'openslp')
source=(${pkgname}-${pkgver}.tar.bz2::"http://quickgit.kde.org/?p=kde-runtime.git&a=snapshot&h=cbff222cf1d04ec3fa93c76f0acd3db93edebfb0&fmt=tbz2"
        'ecm.patch'
        'kcurrencycode.patch')
md5sums=('SKIP'
         '1f52720a74d0988269f5b965a748f9f0'
         'caa9fe4a05ef62859d7116073a84b15c')

prepare() {
  mkdir -p build

  cd ${pkgname}
  patch -p1 -i "${srcdir}"/ecm.patch
  patch -p1 -i "${srcdir}"/kcurrencycode.patch
}

build() {
  cd build
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
