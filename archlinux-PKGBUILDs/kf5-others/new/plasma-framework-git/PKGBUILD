# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework-git
pkgver=plasma2tp.r61.g4484af5
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/libs/plasma-framework'
license=('LGPL')
depends=('qt5-quick1' 'kidletime-git' 'kitemmodels-git'
         'kwidgetsaddons-git' 'kcodecs-git' 'karchive-git' 'solid-git' 'threadweaver-git'
         'kglobalaccel-git' 'kauth-git' 'kwallet-framework-git' 'kguiaddons-git'
         'kconfigwidgets-git' 'kitemviews-git' 'knotifications-git' 'kiconthemes-git'
         'kcompletion-git' 'kjobwidgets-git' 'sonnet-git' 'ktextwidgets-git' 'kxmlgui-git'
         'kbookmarks-git' 'kdeclarative-git' 'kunitconversion-git' 'kparts-git' 'kross-git'
         'kdnssd-framework-git' 'kde4support-git' 'kdesignerplugin-git' 'kinit-git'
         'kactivities-frameworks-git')
makedepends=('extra-cmake-modules-git' 'git' 'qt5-tools')
conflicts=('plasma-framework')
provides=('plasma-framework')
source=('git://anongit.kde.org/plasma-framework.git')
md5sums=('SKIP')

pkgver() {
  cd plasma-framework
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  export CMAKE_PREFIX_PATH="/opt/kf5"

  cd build
  cmake ../plasma-framework \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
