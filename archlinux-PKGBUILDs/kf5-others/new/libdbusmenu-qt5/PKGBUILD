# $Id: PKGBUILD 178965 2013-03-01 10:21:35Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libdbusmenu-qt5
pkgver=0.9.2daily13.03.28
pkgrel=1
pkgdesc="A library that provides a Qt implementation of the DBusMenu spec"
arch=('i686' 'x86_64')
url="https://launchpad.net/libdbusmenu-qt"
license=('GPL')
depends=('qt5-base')
makedepends=('cmake')
source=("http://archive.ubuntu.com/ubuntu/pool/main/libd/libdbusmenu-qt/libdbusmenu-qt_${pkgver}.orig.tar.gz")
md5sums=('c83e42cdc9e53d6803b149af26c07a42')
options=(strip debug)

build() {
  mkdir -p build
  cd build
  cmake ../libdbusmenu-qt-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_QT5=On \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_DOC=Off
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
