# $Id: PKGBUILD 202380 2013-12-21 14:03:17Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesignerplugin-git
pkgver=r20.cf6639a
pkgrel=1
pkgdesc='KDesignerPlugin'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdesignerplugin'
license=('LGPL')
depends=('kio-git' 'kplotting-git' 'kdewebkit-git')
makedepends=('extra-cmake-modules-git' 'git' 'qt5-tools' 'kdoctools-git')
groups=('kf5')
conflicts=(kdesignerplugin)
provides=(kdesignerplugin)
source=('git://anongit.kde.org/kdesignerplugin.git')
md5sums=('SKIP')

pkgver() {
  cd kdesignerplugin
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kdesignerplugin \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
