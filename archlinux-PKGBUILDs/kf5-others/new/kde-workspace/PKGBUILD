# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-workspace
pkgver=0.plasma2tp
pkgrel=2
pkgdesc='Provides the interface and basic tools for the KDE workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('frameworkintegration' 'kcmutils' 'knotifyconfig' 'kprintutils'
         'kdesu' 'knewstuff' 'khtml' 'libdbusmenu-qt5' 'glu'
         'libxkbfile' 'libxcursor' 'lm_sensors' 'libraw1394' 'libusb-compat'
         'pciutils' 'libbsd' 'kf5-akonadi-git' 'qt5-webkit')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
source=(${pkgname}-${pkgver}.tar.bz2::"http://quickgit.kde.org/?p=kde-workspace.git&a=snapshot&h=4ba62f3059b3243ca3cd997a07af80555a3f4265&fmt=tbz2"
        'presentwindows.patch'
        'kglobalaccel.patch'
        'kstyle.patch'
        'kstyle2.patch'
        'knotifyconfig.patch')
md5sums=('SKIP'
         'fd1a81676059ce7a8117ce0bb91a706a'
         'c966db3a7a532eeacff52767ce045a8d'
         'b3c4bb931592092a70b7a4ac301df348'
         '3540a5325a25e9bfd914d0d3c4eb7019'
         'f952c70b34b5ced3c895da7fa20c7357')

prepare() {
  mkdir -p build

  cd ${pkgname}
  patch -p1 -i "${srcdir}"/presentwindows.patch
  patch -p1 -i "${srcdir}"/kglobalaccel.patch
  patch -p1 -i "${srcdir}"/kstyle.patch
  patch -p1 -i "${srcdir}"/kstyle2.patch
  patch -p1 -i "${srcdir}"/knotifyconfig.patch
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"
  export CMAKE_PREFIX_PATH="/opt/kf5"

  cd build
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF \
    -DWITH_libgps=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
