# $Id: PKGBUILD 202381 2013-12-21 14:07:21Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=solid-git
pkgver=r5.6038b79
pkgrel=1
pkgdesc='Solid'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/solid'
license=('LGPL')
depends=('qt5-declarative' 'media-player-info' 'upower' 'udisks2')
makedepends=('extra-cmake-modules-git' 'git')
groups=('kf5')
conflicts=(solid)
provides=(solid)
source=('git://anongit.kde.org/solid.git')
md5sums=('SKIP')

pkgver() {
  cd solid
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../solid \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF \
    -DHUPNP_ENABLED=FALSE
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
