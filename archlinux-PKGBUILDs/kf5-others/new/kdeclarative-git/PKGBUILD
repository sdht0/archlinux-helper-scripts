# $Id: PKGBUILD 202287 2013-12-21 09:59:45Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeclarative-git
pkgver=r5.1820e76
pkgrel=1
pkgdesc='KDeclarative'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdeclarative'
license=('LGPL')
depends=('kio-git')
makedepends=('extra-cmake-modules-git' 'git')
groups=('kf5')
conflicts=(kdeclarative)
provides=(kdeclarative)
source=('git://anongit.kde.org/kdeclarative.git')
md5sums=('SKIP')

pkgver() {
  cd kdeclarative
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdeclarative \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
