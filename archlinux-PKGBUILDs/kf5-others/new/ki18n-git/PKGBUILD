# $Id: PKGBUILD 202326 2013-12-21 12:12:30Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=ki18n-git
pkgver=r5.d995b94
pkgrel=1
pkgdesc='Ki18n'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/ki18n'
license=('LGPL')
depends=('kjs-git')
makedepends=('extra-cmake-modules-git' 'git')
groups=('kf5')
conflicts=(ki18n)
provides=(ki18n)
source=('git://anongit.kde.org/ki18n.git')
md5sums=('SKIP')

pkgver() {
  cd ki18n
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ki18n \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
