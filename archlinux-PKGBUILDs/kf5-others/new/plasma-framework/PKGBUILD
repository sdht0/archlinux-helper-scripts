# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=0.plasma2tp
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/libs/plasma-framework'
license=('LGPL')
depends=('qt5-quickcontrols' 'kidletime' 'kitemmodels' 'threadweaver'
         'kwallet-framework' 'kdeclarative' 'kunitconversion'
         'kross' 'kdnssd-framework' 'kde4support' 'kinit'
         'kactivities-frameworks-git' 'qt5-quick1')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
optdepends=('kde-workspace: provide a Plasma shell')
source=(${pkgname}-${pkgver}.tar.bz2::"http://quickgit.kde.org/?p=plasma-framework.git&a=snapshot&h=d2992acfa72c6a3aec848f1f766196023270161f&fmt=tbz2"
        'kdnssd.patch'
        'threadweaver.patch')
md5sums=('SKIP'
         '129069b582053f2b57228a872ab74b3b'
         'bd9dbb036909a2177464e7ba2b835ade')

prepare() {
  mkdir -p build

  cd plasma-framework
  patch -p1 -i "${srcdir}"/kdnssd.patch
  patch -p1 -i "${srcdir}"/threadweaver.patch
}

build() {
  export CMAKE_PREFIX_PATH="/opt/kf5"

  cd build
  cmake ../plasma-framework \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
