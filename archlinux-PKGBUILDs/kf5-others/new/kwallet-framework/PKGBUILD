# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet-framework
pkgver=4.95.0
pkgrel=1
pkgdesc='KWallet Framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kwallet-framework'
license=('LGPL')
depends=('kwindowsystem' 'kconfig')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('611b8cf450f5cf67620ee6935ed52484')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
