
pkgname=kf5-kfileaudiopreview-git
_pkgname=kfileaudiopreview
pkgver=5.a4c4c68
pkgrel=1
pkgdesc="KFile Audio Preview"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/frameworks/kfileaudiopreview"
license=('LGPL')
depends=('kde5-phonon-qt5-git' 'kf5-kio-git')
makedepends=('extra-cmake-modules-git' 'git')
conflicts=("kf5-${_pkgname}" "${_pkgname}-git" "${_pkgname}")
provides=("kf5-${_pkgname}" "${_pkgname}")
source=("git://anongit.kde.org/kfileaudiopreview")
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../"${_pkgname}" \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Debug \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
