# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kf5-phonon-git
pkgver=v4.7.1.7.g2adbd9d
pkgrel=1
pkgdesc='The multimedia framework for KDE'
arch=('i686' 'x86_64')
url='http://phonon.kde.org/'
license=('LGPL')
depends=('qt5-base' 'phonon-backend' 'libpulse')
optdepends=('pulseaudio: PulseAudio support'
            'qt5-tools: QtDesigner Phonon plugin')
makedepends=('cmake-git' 'pulseaudio' 'git' 'qt5-quick1' 'qt5-tools')
provides=('kf5-phonon')
conflicts=('kf5-phonon')
options=('debug')
source=(git://anongit.kde.org/phonon.git)
md5sums=('SKIP')

pkgver() {
  cd phonon
  git describe --always | sed 's|-|.|g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  /opt/kf5/bin/cmake ../phonon \
    -DCMAKE_PREFIX_PATH=/opt/kf5 \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=DebugFull \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON \
    -DPHONON_BUILD_PHONON4QT5=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
