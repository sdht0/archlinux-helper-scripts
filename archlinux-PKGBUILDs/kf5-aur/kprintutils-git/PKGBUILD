# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kprintutils-git
pkgver=r7.cc8aa68
pkgrel=1
pkgdesc='KPrintUtils'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kprintutils'
license=('LGPL')
depends=('kparts-git')
makedepends=('extra-cmake-modules-git' 'git')
groups=('kf5')
conflicts=(kprintutils)
provides=(kprintutils)
source=('git://anongit.kde.org/kprintutils.git')
md5sums=('SKIP')

pkgver() {
  cd kprintutils
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kprintutils \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
