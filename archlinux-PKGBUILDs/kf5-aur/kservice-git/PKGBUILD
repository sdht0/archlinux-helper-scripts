# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kservice-git
pkgver=r19.8ab2dbf
pkgrel=1
pkgdesc='KService'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kservice'
license=('LGPL')
depends=('ki18n-git' 'kconfig-git' 'kcrash-git' 'kdbusaddons-git')
makedepends=('extra-cmake-modules-git' 'git' 'kdoctools-git')
groups=('kf5')
conflicts=(kservice)
provides=(kservice)
source=('git://anongit.kde.org/kservice.git')
md5sums=('SKIP')

pkgver() {
  cd kservice
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kservice \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
