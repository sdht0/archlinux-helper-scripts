# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor-git
pkgver=r40.g470e9f7
pkgrel=1
pkgdesc='KTextEditor framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/ktexteditor'
license=('LGPL')
depends=('knotifications-git' 'kprintutils-git' 'karchive-git'
         'kconfig-git' 'kguiaddons-git' 'ki18n-git' 'kjobwidgets-git'
         'kio-git' 'kparts-git' 'sonnet-git' 'kxmlgui-git')
makedepends=('extra-cmake-modules-git' 'git')
source=('git://anongit.kde.org/ktexteditor.git')
md5sums=('SKIP')

pkgver() {
  cd ktexteditor
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../ktexteditor \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
