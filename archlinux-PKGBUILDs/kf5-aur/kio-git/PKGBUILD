# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-git
pkgver=r42.bfb7586
pkgrel=1
pkgdesc='KIO'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kio'
license=('LGPL')
depends=('krb5' 'solid-git' 'knotifications-git' 'kjobwidgets-git' 'kbookmarks-git'
         'qt5-script')
makedepends=('extra-cmake-modules-git' 'git' 'kdoctools-git')
groups=('kf5')
conflicts=(kio)
provides=(kio)
install=kio.install
source=('git://anongit.kde.org/kio.git')
md5sums=('SKIP')

pkgver() {
  cd kio
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kio \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
