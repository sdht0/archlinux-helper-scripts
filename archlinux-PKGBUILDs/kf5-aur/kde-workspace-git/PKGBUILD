# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-workspace-git
pkgver=plasma2tp.r194.g9521895
pkgrel=1
pkgdesc='Provides the interface and basic tools for the KDE workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('frameworkintegration-git' 'kcmutils-git' 'knotifyconfig-git' 'kprintutils-git'
         'kdesu-git' 'knewstuff-git' 'khtml-git' 'libdbusmenu-qt5' 'qimageblitz'
         'libxkbfile' 'libxcursor' 'lm_sensors' 'libraw1394' 'libusb-compat'
         'pciutils' 'libbsd' 'kf5-akonadi-git'
         'plasma-framework-git' 'kdoctools-git' 'kde4support-git')
makedepends=('extra-cmake-modules-git' 'git' 'qt5-tools')
conflicts=('kde-workspace')
provides=('kde-workspace')
source=('git://anongit.kde.org/kde-workspace.git')
md5sums=('SKIP')

pkgver() {
  cd kde-workspace
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"
  export CMAKE_PREFIX_PATH="/opt/kf5"

  cd build
  cmake ../kde-workspace \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
