# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kconfigwidgets-git
pkgver=r8.ade928f
pkgrel=1
pkgdesc='Widgets for KConfig'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kconfigwidgets'
license=('LGPL')
depends=('kauth-git' 'kcodecs-git' 'kconfig-git' 'karchive-git' 'kguiaddons-git' 'ki18n-git'
         'kwidgetsaddons-git')
makedepends=('extra-cmake-modules-git' 'git' 'kdoctools-git')
groups=('kf5')
conflicts=(kconfigwidgets)
provides=(kconfigwidgets)
source=('git://anongit.kde.org/kconfigwidgets.git')
md5sums=('SKIP')

pkgver() {
  cd kconfigwidgets
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kconfigwidgets \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
