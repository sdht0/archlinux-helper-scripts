# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kjsembed-git
pkgver=r6.3af3432
pkgrel=1
pkgdesc='KJSEmbed'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kjsembed'
license=('LGPL')
depends=('ki18n-git' 'qt5-svg')
makedepends=('extra-cmake-modules-git' 'git' 'qt5-tools' 'kdoctools-git')
groups=('kf5')
conflicts=(kjsembed)
provides=(kjsembed)
source=('git://anongit.kde.org/kjsembed.git')
md5sums=('SKIP')

pkgver() {
  cd kjsembed
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kjsembed \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
