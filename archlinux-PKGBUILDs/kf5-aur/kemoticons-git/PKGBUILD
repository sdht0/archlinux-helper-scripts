# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kemoticons-git
pkgver=r8.8568490
pkgrel=1
pkgdesc='KEmoticons'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kemoticons'
license=('LGPL')
depends=('karchive-git' 'kservice-git')
makedepends=('extra-cmake-modules-git' 'git')
groups=('kf5')
conflicts=(kemoticons)
provides=(kemoticons)
source=('git://anongit.kde.org/kemoticons.git')
md5sums=('SKIP')

pkgver() {
  cd kemoticons
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kemoticons \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
