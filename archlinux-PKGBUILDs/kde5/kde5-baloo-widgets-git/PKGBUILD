# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-widgets-git
_pkgname=baloo-widgets
pkgver=r373.b511d20
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('baloo-git')
makedepends=('kde5-extra-cmake-modules-git' 'git' 'kdoctools')
source=('git://anongit.kde.org/baloo-widgets')
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${_pkgname} \
        -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
        -DCMAKE_BUILD_TYPE=Debug \
        -DLIB_INSTALL_DIR=lib
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
