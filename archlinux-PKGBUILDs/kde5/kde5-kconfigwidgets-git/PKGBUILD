
pkgname=kde5-kconfigwidgets-git
_pkgname=kconfigwidgets
pkgver=1
pkgrel=1
pkgdesc="Widgets for KConfig"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/frameworks/kconfigwidgets"
license=('LGPL')
depends=('kde5-kauth-git' 'kde5-kcodecs-git' 'kde5-kconfig-git' 'kde5-kdoctools-git' 'kde5-kguiaddons-git' 'kde5-kwidgetsaddons-git')
makedepends=('kde5-extra-cmake-modules-git' 'git')
group=("kde5")
source=("git://anongit.kde.org/kconfigwidgets")
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    mkdir -p build
}

build() {
    export PATH="/opt/kde5/bin:$PATH"
    export XDG_DATA_DIRS="/opt/kde5/share:$XDG_DATA_DIRS"

    cd build
    cmake ../"${_pkgname}" \
        -DCMAKE_INSTALL_PREFIX=/opt/kde5 \
        -DCMAKE_BUILD_TYPE=Debug \
        -DLIB_INSTALL_DIR=lib
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
