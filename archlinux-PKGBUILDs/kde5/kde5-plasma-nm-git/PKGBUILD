
pkgname=kde5-plasma-nm-git
_pkgname=plasma-nm
pkgver=1
pkgrel=1
pkgdesc="Plasma applet written in QML for managing network connections"
arch=('i686' 'x86_64')
url="http://projects.kde.org/projects/kde/workspace/plasma-nm"
license=('LGPL')
depends=('kde5-libmm-qt-git' 'kde5-networkmanager-qt-git' 'kde5-plasma-framework-git')
makedepends=('kde5-extra-cmake-modules-git' 'git')
group=("kde5")
source=("git://anongit.kde.org/plasma-nm")
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    mkdir -p build
}

build() {
    export PATH="/opt/kde5/bin:$PATH"
    export XDG_DATA_DIRS="/opt/kde5/share:$XDG_DATA_DIRS"

    cd build
    cmake ../"${_pkgname}" \
        -DCMAKE_INSTALL_PREFIX=/opt/kde5 \
        -DCMAKE_BUILD_TYPE=Debug \
        -DLIB_INSTALL_DIR=lib
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
