# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=akonadi-search-git
_pkgname=akonadi-search
pkgver=r418.bdefb90
pkgrel=1
pkgdesc="Libraries and daemons to implement searching in Akonadi. (GIT version)"
url='https://projects.kde.org/projects/playground/pim/akonadi-search'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('xapian-core' 'kde5-kdepimlibs-git')
makedepends=('kde5-extra-cmake-modules-git' 'kde5-kdoctools-git' 'git')
source=("git://anongit.kde.org/akonadi-search")
sha1sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Debug \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}

