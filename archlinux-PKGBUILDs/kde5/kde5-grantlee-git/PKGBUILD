# Maintainer: Wouter Haffmans <wouter@simply-life.net>
pkgname=grantlee-git
_pkgname=grantlee
pkgver=v5.0.0.0.g95d9717
pkgrel=1
pkgdesc="Grantlee is a string template engine based on the Django template system."
arch=('i686' 'x86_64')
url="http://www.grantlee.org"
license=('LGPL2.1')
depends=('qt5-base' 'qt5-script')
makedepends=('git' 'cmake')
source=('git://gitorious.org/grantlee/grantlee.git')
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${_pkgname} \
        -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
        -DCMAKE_BUILD_TYPE=Debug \
        -DLIB_INSTALL_DIR=lib
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}