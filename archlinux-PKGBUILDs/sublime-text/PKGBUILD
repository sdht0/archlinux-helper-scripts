# Maintainer: Josh Kropf <josh@slashdev.ca>
# Contributor: Bartosz Chmura <chmurli at gmail dot com>
# Contributor: Mikkel Kroman <mk at maero dot dk>

pkgname=sublime-text
pkgver=2.0.2
pkgrel=1
pkgdesc="sophisticated text editor for code, html and prose"
arch=('i686' 'x86_64')
url="http://www.sublimetext.com/2"
license=('custom')
depends=(libpng gtk2 xdg-utils desktop-file-utils)
install=${pkgname}.install
_arch=' x64'
_archurl='%20x64'

[[ "${CARCH}" = i686 ]] && _arch='' && _archurl=''

source=(
	"http://c758482.r82.cf2.rackcdn.com/Sublime%20Text%20${pkgver}${_archurl}.tar.bz2"
	sublime-text.desktop
	subl
)

md5sums=(
	'699cd26d7fe0bada29eb1b2cd7b50e4b'
	'f6d0e7f5da0e8c1d57640f62173295bf'
	'6d487b24e76dd1a6ba6efed841db6731'
)

[[ "${CARCH}" = i686 ]] && md5sums[0]='34a60dac42f4ebc41218398fcfa92fe2'

package () {
	cd "${srcdir}"

	install -Dm644 ${pkgname}.desktop \
		"${pkgdir}/usr/share/applications/${pkgname}.desktop"

	install -dm755 "${pkgdir}/opt"
	cp --preserve=mode -r "Sublime Text 2" \
		"${pkgdir}/opt/${pkgname}"

	for res in 128x128 16x16 256x256 32x32 48x48; do
		install -dm755 "${pkgdir}/usr/share/icons/hicolor/${res}/apps"
		ln -s /opt/${pkgname}/Icon/${res}/sublime_text.png \
			"${pkgdir}/usr/share/icons/hicolor/${res}/apps/sublime_text.png"
	done

	install -Dm755 subl "${pkgdir}/usr/bin/subl"
}
