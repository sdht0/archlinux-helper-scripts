# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=verlihub-git
pkgver=514a92e
pkgrel=1
pkgdesc="A DC++ protocol server (hub) - GIT version"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.verlihub-project.org/"
depends=('geoip' 'mariadb' 'gettext' 'pcre' 'zlib' 'openssl' 'lua' 'python2')
makedepends=('git' 'cmake')
conflicts=('verlihub')
provides=('verlihub')
source=('git://github.com/VerliHub/verlihub.git'
        'python2.patch')
md5sums=('SKIP'
         '93d5288c62d0ba40930ad9cb234d1fdd')

_gitname="verlihub"

pkgver() {
  cd $_gitname
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

prepare() {
    cd $_gitname
    patch -p1 -i $srcdir/python2.patch
}

build() {
    cd $_gitname
    ./configure --prefix=/usr
    make
}

package() {
    cd $_gitname
    make DESTDIR="${pkgdir}" install
}
